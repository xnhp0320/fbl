# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/test.gni")

group("tests") {
  testonly = true
  deps = [
    ":zxtest-context($host_toolchain)",
    ":zxtest-integration($host_toolchain)",
    ":zxtest-integration-stream-test-pkg",
    ":zxtest-integration-streams($host_toolchain)",
    ":zxtest-integration-test-pkg",
  ]
}

if (is_host) {
  test("zxtest-context") {
    output_name = "zxtest-context-test"
    sources = [ "context_test.cc" ]
    deps = [ "//zircon/system/ulib/zxtest" ]
  }
}

test("zxtest-integration") {
  output_name = "zxtest-integration-test"
  if (is_fuchsia) {
    fdio_config = [ "//build/config/fuchsia:fdio_config" ]
    if (configs + fdio_config - fdio_config != configs) {
      configs -= fdio_config
    }
  }
  sources = [
    "assertions_test.c",
    "assertions_test.cc",
    "helper.cc",
    "main.cc",
    "print_test.c",
    "print_test.cc",
    "printvalue_test.cc",
    "register_test.c",
    "register_test.cc",
  ]
  deps = [
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/zxtest",
  ]
  if (is_fuchsia) {
    deps += [
      "//sdk/lib/fdio",
      "//zircon/system/ulib/zx",
    ]
  }
}

test("zxtest-integration-streams") {
  output_name = "zxtest-integration-streams-test"
  if (is_fuchsia) {
    fdio_config = [ "//build/config/fuchsia:fdio_config" ]
    if (configs + fdio_config - fdio_config != configs) {
      configs -= fdio_config
    }
  }
  sources = [
    "assertions_streams_test.cc",
    "helper.cc",
    "main.cc",
  ]
  deps = [
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/zxtest:zxtest-streams",
  ]
  if (is_fuchsia) {
    deps += [
      "//sdk/lib/fdio",
      "//zircon/system/ulib/zx",
    ]
  }
}

fuchsia_unittest_package("zxtest-integration-test-pkg") {
  package_name = "zxtest-integration-test"
  deps = [ ":zxtest-integration" ]
}

fuchsia_unittest_package("zxtest-integration-stream-test-pkg") {
  package_name = "zxtest-integration-stream-test"
  deps = [ ":zxtest-integration-streams" ]
}
